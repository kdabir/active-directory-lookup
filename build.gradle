plugins {
    id 'java'
    id 'application'
    id 'maven-publish'

    id 'jacoco'
    id "com.github.spotbugs" version "5.0.11"
}


group = "io.github.kdabir.adl"
description = "A Java Library/Application to access Active Directory"
mainClassName = "io.github.kdabir.adl.cli.ActiveDirectoryCLI"

sourceSets {
    integrationTest {
        java.srcDir 'src/integrationTest/java'
        resources.srcDir 'src/integrationTest/resources'
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

repositories { mavenCentral() }

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-all:1.10.19'

    integrationTestImplementation 'junit:junit:4.12'
}


task integrationTests(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.getClassesDirs()
    classpath += sourceSets.integrationTest.runtimeClasspath
}

jacocoTestReport {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

build.dependsOn javadocJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
        }
    }
}


